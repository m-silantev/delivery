plugins {
    alias(libs.plugins.lombok)
    id "com.google.protobuf" version "0.9.4"
}

dependencies {
    compileOnly project(':core')
    implementation libs.spring.boot.starter.data.jdbc
    implementation libs.postgres.driver
    implementation libs.liquibase

    implementation 'com.google.protobuf:protobuf-java:4.28.3'
    implementation 'io.grpc:grpc-stub:1.68.1'
    implementation 'io.grpc:grpc-protobuf:1.68.1'
    implementation 'io.grpc:grpc-netty-shaded:1.68.1'
    implementation 'javax.annotation:javax.annotation-api:1.3.1'

    testImplementation project(':core')
    testImplementation libs.bundles.testcontainers
    testImplementation libs.spring.boot.test
}

protobuf {
    protoc {
        // The artifact spec for the Protobuf Compiler
        artifact = 'com.google.protobuf:protoc:4.28.3'
    }
    plugins {
        // Optional: an artifact spec for a protoc plugin, with "grpc" as
        // the identifier, which can be referred to in the "plugins"
        // container of the "generateProtoTasks" closure.
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.68.1'
        }
    }
    generateProtoTasks {
        ofSourceSet('main').configureEach {
            plugins {
                // Apply the "grpc" plugin whose spec is defined above, without
                // options.  Note the braces cannot be omitted, otherwise the
                // plugin will not be added. This is because of the implicit way
                // NamedDomainObjectContainer binds the methods.
                grpc { }
            }
        }
    }
}
